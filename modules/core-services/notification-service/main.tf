resource "google_sql_database" "notification-service-db" {
  name     = "${var.service_name}-db"
  instance = var.cloud_sql_instance_name
}

resource "google_service_account" "notification-service-service-account" {
  account_id   = "${var.namespace}-service-account"
  display_name = "Service Account for ${var.service_name} to use Google PubSub"
}

resource "google_service_account_key" "notification-service-service-account-key" {
  service_account_id = google_service_account.notification-service-service-account.name
  public_key_type    = "TYPE_X509_PEM_FILE"
}

resource "google_project_iam_member" "notification-service-service-account-iam-member" {
  project            = var.project_id
  role               = "roles/pubsub.admin"
  member             = "serviceAccount:${google_service_account.notification-service-service-account.email}"
}

resource "kubernetes_secret" "notification-service-service-account-credentials" {
  metadata {
    name = "notification-service-service-account-credentials"
    namespace = var.namespace
  }
  data = {
    GOOGLE_APPLICATION_CREDENTIALS = base64decode(google_service_account_key.notification-service-service-account-key.private_key)
  }
}

resource "kubernetes_secret" "firebase-database-url" {
  metadata {
    name = "firebase-database-url"
    namespace = var.namespace
  }

  data = {
    FIREBASE_DATABASE_URL = var.firebase_database_url
  }
}

# Replace the service account name appropriately according to the one generated by firebase project
# For more info, refer to https://console.firebase.google.com/u/1/project/place-2-meet-dev/settings/serviceaccounts/adminsdk
resource "google_service_account_key" "firebase-admin-service-account-key" {
  service_account_id = var.firebase_admin_service_account
  public_key_type    = "TYPE_X509_PEM_FILE"
}

resource "kubernetes_secret" "firebase-admin-service-account-credentials" {
  metadata {
    name = "firebase-admin-service-account-credentials"
    namespace = var.namespace
  }
  data = {
    FIREBASE_APPLICATION_CREDENTIALS = base64decode(google_service_account_key.firebase-admin-service-account-key.private_key)
  }
}